( ASSEMBLER CDP 1802 FUER IPS-C3 )

:PRIOR I> 0 COMPILEFLAG !B ;
:INT <I 1 COMPILEFLAG !B ; 0 VAR $V2
: ERZ-ENDE R>S $V2 @ ! ;
: , HIER !B $H INCR ;
:PRIOR MACHT> ' ERZ-ENDE $DEP #D5 , ;
: ERZ> ENTRYSETUP JA? $V2 !
NEIN: R>S WEG
DANN ;
:INT RCODE ENTRYSETUP JA? ! DANN ;
:INT CODE ENTRYSETUP JA? HIER VERT !
DANN ;
16 FELD $JERROR " SPRUNGZ.N.A.P.! " $JERROR !T
16 FELD $CERROR " VORBEF. UNZUL.! " $CERROR !T
: BDEP ERZ> , MACHT> @B , ;
: $TERM WEG $CERROR SYSWRITE ;
: $INADR PDUP EXO #FF00 UND
=0 JA? !B
NEIN: WEG $JERROR SYSWRITE
DANN ;
: $AD ZWO #FFF0 UND =0 JA? + ,
NEIN: $TERM
DANN ;
: $ADO ERZ> , MACHT> @B $AD ;
: $ALUACT ZWO #FF00 UND #A500 =
JA? VERT $AD
NEIN: $TERM
DANN ;
: MX 0 $ALUACT ; : IM 8 $ALUACT , ;
#00 $ADO LDN #70 BDEP RET #A574 KON ADC
#10 $ADO INC #71 BDEP DIS #A575 KON -DC
#20 $ADO DEC #72 BDEP LDXA #A577 KON DC-
#40 $ADO LDA #73 BDEP STXD #A5F0 KON LD
#50 $ADO STR #76 BDEP ROR #A5F1 KON OR
#60 $ADO I/O #78 BDEP SAV #A5F2 KON AND
#80 $ADO GLO #79 BDEP MARK #A5F3 KON XOR
#90 $ADO GHI #7A BDEP 0>Q #A5F4 KON ADD
#A0 $ADO PLO #7B BDEP 1>Q #A5F5 KON -D
#B0 $ADO PHI #7E BDEP ROL #A5F7 KON D-
#D0 $ADO ->P #C4 BDEP NOP #0002 KON RS
#E0 $ADO ->X #C8 BDEP SKP2 #0000 KON NEVER
#CC BDEP LSIE #0009 KON Q=1
#00 BDEP IDL #F6 BDEP SHR #000A KON D=0
#38 BDEP SKP #FE BDEP SHL #000B KON DF
#60 BDEP INCX #D6 BDEP NEXT #000B KON PS
: NOT 8 EXO ; : EF 11 + ; : BEGIN HIER ;
: Y? #30 $AD HIER $H INCR ; : TH HIER VERT $INADR ;
: N: 0 Y? VERT TH ; : END Y? $INADR ;
: $SBRT ZWO 4 UND >0 JA? VERT
DANN $AD ;
: LEND #C0 $SBRT DUP 256 / , , ;
: LSKP #C4 $SBRT ;
